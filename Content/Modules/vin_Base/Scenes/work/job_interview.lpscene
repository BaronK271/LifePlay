WHAT: none
WHERE: none
WHEN: 0-24
WHO: none
OTHER: none

sceneStart()

Difficulty = Random(0.25, 2)
Salary = Difficulty*(1500+jobexperience*100)
SalaryConverted = Salary.convertToLocalCurrency(true)

"I discovered a open vacancy at this company for someone with my experience. I calculated that I could take home around <SalaryConverted> per month with this job."

"Should I apply for this position?"
0:: "Yes"
1:: "No"

If 0
    "I spent some time completing application forms and tailoring my CV and cover letter to the job ad. I'm fairly happy with what I currently have. However, I reckon certain 'embellishments' on my application would do well for my success chance."
    0:: "Be honest"
    1:: "Lie a bit"
    
    If 0
        "I remained honest in everything I wrote and submitted my application"
        LieOnCV = false
    Else
        "I added a few believable lies to make myself look better as a candidate, then submitted my application"
        LieOnCV = true
    endif
    
    If LieOnCV
        Dice = Random(0, 0.7)
    Else
        Dice = Random(0, 1)
    Endif
    
    "A couple of weeks later ..."
    
    passTime(300, 360)
    
    If Dice*(1+Difficulty) < 0.005*(intelligence+interpersonal)
        Player(Happy):: "Thank you for your call. I'm looking forward to the upcoming interview." 
        "I received a call from the HR department, notifying me that my application was successful and inviting me for an interview. I must now prepare for the fateful day."
        If LieOnCV
            "I can't know for sure, but perhaps my lies on the application did their magic. Not that they don't carry certain risks ..."
        endif
        
            Boss = generatePerson()

            If Random (0,1) < 0.7
                Random
                    Boss.blendPreset(workaholic)
                    Boss.blendPreset(workhard_playhard)
                endRandom
            endif

            If Random(0,1) < 0.95
                Random
                    Boss.blendPreset(fourties)
                    Boss.blendPreset(fifties)
                    Boss.blendPreset(sixties)        
                endRandom
            endif
                
            Boss.randomizeFace()
            Boss.randomizeHairs()
                
            Boss.dress()
            Boss.show(2)

        
    Else
        Player(Sad):: "Thank you for your time anyway. All the best with finding the right candidate."
        "Oh no! The HR department just called. My application was unsuccessful. I didn't even manage to secure an interview. I must now look for alternative positions elsewhere."
        If !LieOnCV
            "On hindsight, maybe being so honest wasn't a good idea after all."
        endif
    Endif
    
Else
    "I decided against applying for this position."
endif

sceneEnd()